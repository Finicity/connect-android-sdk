apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.6' //Use latest version
}
tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"
    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = true
    }
    def fileFilter = ['**/R.class',
                      '**/R$*.class', '**/*$InjectAdapter.*',
                      '**/*$ModuleAdapter.*',
                      '**/Dao*.*',
                      '**/*$ViewInjector*.*',
                      '**/*$ViewBinder*.*',
                      '**/*MembersInjector*.*',
                      '**/BuildConfig.*',
                      '**/Manifest*.*',
                      '**/*$Lambda$*.',
                      '**/*Factory*.',
                      '**/*$Builder*',
                      '**/*$Parcelable*.*',
                      '**/*DaggerApplicationComponent*.*',
                      '**/api',
                      '**/android/**/*.*',
                      '**/me/**/*.*', '**/org/**/*.*', '**/org/**/*.*']

    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/debug/classes/", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java/"
    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])
    executionData.from = fileTree(dir: project.projectDir, includes:
            ['**/**/*.exec', '**/**/*.ec'])
}

apply from: "$project.rootDir/sonar.gradle"

def libraryGroupId = 'com.finicity.connect'
def libraryArtifactId = 'connect-sdk'
def libraryVersion = '1.0.4'

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 104
        versionName "1.0.4"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.22'
    testImplementation 'org.robolectric:robolectric:4.3'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-web:3.0.2'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                groupId libraryGroupId
                artifactId libraryArtifactId
                version libraryVersion
                artifact androidSourcesJar
                artifact bundleReleaseAar

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}

/*
    To clean, build aar, generate pom, and to upload aar/pom to bintray issue the following command from terminal.
    ./gradlew connect-sdk:clean connect-sdk:assembleRelease connect-sdk:generatePomFileForLibraryPublication connect-sdk:bintrayUpload
 */

// Bintray properties for accessing bintray.  If we open source then move to local.properties
// or gradle.properties that is not in source code control

ext {
    bintrayOrg = "finicity"
    bintrayRepo = "connect-android-sdk"
    bintrayPackage = "com.finicity.connect"
    bintrayPackageDesc = "Connect Android SDK"
    bintrayPackageVcsUrl = "https://github.com/Finicity/connect-android-sdk.git"
}

/*
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
*/

bintray {
/*
    user = properties.getProperty('bintrayUser')
    key = properties.getProperty('bintrayApiKey')
*/
    user = System.getenv('BINTRAY_USER') // Get bintray User
    key = System.getenv('BINTRAY_API_KEY') // Get bintray Secret Key
    publications = ['library']
    publish = true
    override = true
    pkg {
        userOrg = bintrayOrg
        repo = bintrayRepo
        name = bintrayPackage
        desc = bintrayPackageDesc
        licenses = ['MIT']
        vcsUrl = bintrayPackageVcsUrl
        publicDownloadNumbers = true
        version {
            name = libraryVersion
            released = new Date()
        }
    }
}
